pragma ever-solidity ^0.62.0;


import "./base/ve_account/VoteEscrowAccountBase.tsol";


contract VoteEscrowAccount is VoteEscrowAccountBase {
    // Cant be deployed directly
    constructor() public { revert(); }

    function upgrade(
        TvmCell new_code, uint32 new_version, Callback.CallMeta meta
    ) external override onlyVoteEscrowOrSelf {
        if (new_version == current_version) {
            tvm.rawReserve(_reserve(), 0);
            meta.send_gas_to.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
            return;
        }

        uint8 _tmp;
        TvmBuilder main_builder;
        main_builder.store(voteEscrow); // address 267
        main_builder.store(_tmp); // 8
        main_builder.store(meta.send_gas_to); // address 267

        TvmCell empty;
        main_builder.storeRef(empty); // ref

        TvmBuilder initial;
        initial.store(user);

        main_builder.storeRef(initial); // ref 2

        TvmBuilder params;
        params.store(new_version);
        params.store(current_version);
        params.store(dao_root);

        main_builder.storeRef(params); // ref3

        TvmCell storage_data = abi.encode(
            qubeBalance,
            veQubeBalance,
            unlockedQubes,
            veQubeAverage,
            veQubeAveragePeriod,
            lastUpdateTime,
            lastEpochVoted,
            activeDeposits,
            deposits
        );
        TvmCell data = abi.encode(meta.call_id, meta.nonce, storage_data);

        main_builder.storeRef(data); // ref3

        tvm.setcode(new_code);
        // run onCodeUpgrade from new code
        tvm.setCurrentCode(new_code);
        onCodeUpgrade(main_builder.toCell());
    }

    function onCodeUpgrade(TvmCell upgrade_data) private {
        TvmSlice s = upgrade_data.toSlice();
        (address root_, , address send_gas_to) = s.decode(address, uint8, address);

        TvmCell _platform_code = s.loadRef();

        TvmSlice initialData = s.loadRefAsSlice();
        address _user = initialData.decode(address);

        TvmSlice params = s.loadRefAsSlice();
        uint32 prev_version;
        uint32 new_version;
        address _dao_root;
        (new_version, prev_version, _dao_root) = params.decode(uint32, uint32, address);

        if (new_version == prev_version) {
            tvm.resetStorage();
            tvm.rawReserve(_reserve(), 0);

            platform_code = _platform_code;
            voteEscrow = root_;
            user = _user;
            current_version = new_version;
            dao_root = _dao_root;

            IVoteEscrow(voteEscrow).onVoteEscrowAccountDeploy{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                user, Callback.CallMeta(0, 0, send_gas_to)
            );
        } else {
            tvm.rawReserve(_reserve(), 0);
            send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
        }
    }
}
