pragma ever-solidity ^0.62.0;


import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "../../interfaces/IVoteEscrowAccount.tsol";
import "./VoteEscrowDAO.tsol";


abstract contract VoteEscrowEpochVoting is VoteEscrowDAO {
    function initialize(uint32 start_offset, Callback.CallMeta meta) external override onlyOwner {
        // codes installed
        require (!platformCode.toSlice().empty(), Errors.CANT_BE_INITIALIZED);
        require (!veAccountCode.toSlice().empty(), Errors.CANT_BE_INITIALIZED);
        // distribution params
        require (distributionSchedule.length > 0, Errors.CANT_BE_INITIALIZED);
        require (distributionScheme.length > 0, Errors.CANT_BE_INITIALIZED);
        // people can buy whitelist
        require (gaugeWhitelistPrice > 0, Errors.CANT_BE_INITIALIZED);
        // voting params were installed
        require (epochTime > 0 && timeBeforeVoting > 0 && votingTime > 0, Errors.CANT_BE_INITIALIZED);
        // additional voting params
        require (gaugeMaxVotesRatio > 0 && maxGaugesPerVote > 0, Errors.CANT_BE_INITIALIZED);
        require (!initialized, Errors.ALREADY_INITIALIZED);

        uint32 start_time = now + start_offset;
        tvm.rawReserve(_reserve(), 0);
        currentEpochStartTime = start_time;
        currentEpochEndTime = start_time + epochTime;
        currentEpoch = 1;
        initialized = true;

        emit Initialize(now, start_time, currentEpochEndTime);
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function setVotingParams(
        uint32 _epoch_time,
        uint32 _time_before_voting,
        uint32 _voting_time,
        uint32 _gauge_min_votes_ratio,
        uint32 _gauge_max_votes_ratio,
        uint8 _gauge_max_downtime,
        uint32 _max_gauges_per_vote,
        Callback.CallMeta meta
    ) external override onlyOwner {
        require (_gauge_min_votes_ratio < _gauge_max_votes_ratio, Errors.BAD_INPUT);
        require (_gauge_max_votes_ratio <= MAX_VOTES_RATIO, Errors.BAD_INPUT);
        require (_voting_time > 0, Errors.BAD_INPUT);
        require (_time_before_voting + _voting_time < _epoch_time, Errors.BAD_INPUT);
        require (_epoch_time > 0, Errors.BAD_INPUT);
        require (_max_gauges_per_vote > 0, Errors.BAD_INPUT);

        tvm.rawReserve(_reserve(), 0);

        epochTime = _epoch_time;
        timeBeforeVoting = _time_before_voting;
        votingTime = _voting_time;
        gaugeMinVotesRatio = _gauge_min_votes_ratio;
        gaugeMaxVotesRatio = _gauge_max_votes_ratio;
        gaugeMaxDowntime = _gauge_max_downtime;
        maxGaugesPerVote = _max_gauges_per_vote;

        emit NewVotingParams(
            meta.call_id,
            epochTime,
            timeBeforeVoting,
            votingTime,
            gaugeMinVotesRatio,
            gaugeMaxVotesRatio,
            gaugeMaxDowntime,
            maxGaugesPerVote
        );
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function setDistributionScheme(uint32[] _new_scheme, Callback.CallMeta meta) external override onlyOwner {
        require (_new_scheme.length == 3, Errors.BAD_INPUT);
        require (_new_scheme[0] + _new_scheme[1] + _new_scheme[2] == DISTRIBUTION_SCHEME_TOTAL, Errors.BAD_INPUT);

        tvm.rawReserve(_reserve(), 0);
        distributionScheme = _new_scheme;

        emit DistributionSchemeUpdate(meta.call_id, _new_scheme);
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function setDistribution(uint128[] _new_distribution, Callback.CallMeta meta) external override onlyOwner {
        tvm.rawReserve(_reserve(), 0);
        distributionSchedule = _new_distribution;

        emit DistributionScheduleUpdate(meta.call_id, _new_distribution);
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function startVoting(Callback.CallMeta meta) external onlyActive {
        require (msg.value >= Gas.MIN_MSG_VALUE, Errors.LOW_MSG_VALUE);

        tvm.rawReserve(_reserve(), 0);
        _tryStartVoting(meta.call_id);

        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function _tryStartVoting(uint32 call_id) internal {
        // if this is true, than someone already started voting
        // dont throw error on duplicate calls
        if (currentVotingStartTime > 0) {
            // emit event otherwise so we can catch function call result on front
            emit VotingStartedAlready(call_id, currentVotingStartTime, currentVotingEndTime);
            return;
        }
        require (initialized, Errors.NOT_INITIALIZED);
        require (currentEpoch - 1 < distributionSchedule.length, Errors.LAST_EPOCH);
        require (now >= currentEpochStartTime + timeBeforeVoting, Errors.TOO_EARLY_FOR_VOTING);

        currentVotingStartTime = now;
        currentVotingEndTime = now + votingTime;
        emit VotingStart(call_id, currentVotingStartTime, currentVotingEndTime);
    }

    // Function for voting with ve qubes user has
    // @param votes - mapping with user votes. Key - gauge address, value - number of ve tokens
    // @param call_id - id helper for front/indexing
    // @param nonce - nonce for callback, ignored if == 0
    // @param send_gas_to - address to send unspent gas
    function voteEpoch(mapping (address => uint128) votes, Callback.CallMeta meta) external onlyActive {
        require (msg.value >= Gas.MIN_MSG_VALUE + maxGaugesPerVote * Gas.PER_GAUGE_VOTE_GAS, Errors.LOW_MSG_VALUE);

        if (currentVotingStartTime == 0) {
            _tryStartVoting(meta.call_id);
        }
        // minimum check for gas dependant on gauges count
        require (currentVotingStartTime > 0, Errors.VOTING_NOT_STARTED);
        require (now <= currentVotingEndTime, Errors.VOTING_ENDED);

        uint32 counter = 0;
        for ((address gauge,) : votes) {
            require (gaugeWhitelist[gauge], Errors.GAUGE_NOT_WHITELISTED);
            require (gaugeDaoApproved[gauge], Errors.GAUGE_NOT_WHITELISTED);
            counter += 1;
        }
        require (counter <= maxGaugesPerVote, Errors.MAX_GAUGES_PER_VOTE);

        tvm.rawReserve(_reserve(), 0);

        address ve_acc_addr = getVoteEscrowAccountAddress(msg.sender);
        IVoteEscrowAccount(ve_acc_addr).processVoteEpoch{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
            currentEpoch, votes, meta
        );
    }

    function finishVote(
        address user, mapping (address => uint128) votes, Callback.CallMeta meta
    ) external override onlyVoteEscrowAccount(user) {
        tvm.rawReserve(_reserve(), 0);

        // this is possible if vote(...) was called right before voting end and data race happen
        if (currentVotingStartTime == 0 || now > currentVotingEndTime) {
            emit VoteRevert(meta.call_id, user);
            _sendCallbackOrGas(user, meta.nonce, false, meta.send_gas_to);
            return;
        }

        for ((address gauge, uint128 vote_value) : votes) {
            currentVotingVotes[gauge] += vote_value;
            currentVotingTotalVotes += vote_value;
        }

        emit Vote(meta.call_id, user, votes);
        _sendCallbackOrGas(user, meta.nonce, true, meta.send_gas_to);
    }

    function revertVote(address user, Callback.CallMeta meta) external override onlyVoteEscrowAccount(user) {
        tvm.rawReserve(_reserve(), 0);

        emit VoteRevert(meta.call_id, user);
        _sendCallbackOrGas(user, meta.nonce, false, meta.send_gas_to);
    }

    function calculateGasForEndVoting() public view returns (uint128 min_gas) {
        min_gas = Gas.MIN_MSG_VALUE + ((gaugesNum / MAX_ITERATIONS_PER_COUNT) + 1) * Gas.GAS_FOR_MAX_ITERATIONS;
        min_gas += Gas.VOTING_TOKEN_TRANSFER_VALUE * math.min((MAX_VOTES_RATIO / gaugeMinVotesRatio) + 1, gaugesNum);
    }

    function endVoting(Callback.CallMeta meta) external onlyActive {
        // make sure we have enough admin deposit to pay for this epoch
        require (distributionSupply >= distributionSchedule[currentEpoch - 1], Errors.LOW_DISTRIBUTION_BALANCE);

        tvm.rawReserve(_reserve(), 0);
        uint128 min_gas = calculateGasForEndVoting();

        // soft fail, because this function could be called simultaneously by several users
        // we dont want require here, because we need to return gas to users which could be really big here
        if (msg.value < min_gas || currentVotingStartTime == 0 || now < currentVotingEndTime) {
            emit VotingEndRevert(meta.call_id);
            meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
            return;
        }

        currentVotingEndTime = 0;
        currentVotingStartTime = 0;
        currentEpoch += 1;
        // if voting ended too late, start epoch now
        currentEpochStartTime = currentEpochEndTime < now ? now : currentEpochEndTime;
        currentEpochEndTime = currentEpochStartTime + epochTime;

        address start_addr = address.makeAddrStd(address(this).wid, 0);
        this.countVotesStep{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(start_addr, 0, 0, meta);
    }

    function countVotesStep(
        address start_addr,
        uint128 exceeded_votes,
        uint128 valid_votes,
        Callback.CallMeta meta
    ) external override {
        require (msg.sender == address(this), Errors.NOT_OWNER);
        tvm.rawReserve(_reserve(), 0);

        bool finished = false;
        uint32 counter = 0;
        uint128 min_votes = currentVotingTotalVotes * gaugeMinVotesRatio / MAX_VOTES_RATIO;
        uint128 max_votes = currentVotingTotalVotes * gaugeMaxVotesRatio / MAX_VOTES_RATIO;

        // no votes at all, set min_votes to 1, so that all gauges get +1 downtime
        if (currentVotingTotalVotes == 0 && gaugeMinVotesRatio > 0) {
            min_votes = 1;
        }

        optional(address, bool) pointer = gaugeWhitelist.nextOrEq(start_addr);
        while (true) {
            if (!pointer.hasValue() || counter >= MAX_ITERATIONS_PER_COUNT) {
                finished = !pointer.hasValue();
                break;
            }

            (address gauge,) = pointer.get();
            uint128 gauge_votes = currentVotingVotes[gauge];

            if (gauge_votes < min_votes) {
                exceeded_votes += gauge_votes;
                delete currentVotingVotes[gauge];
                gaugeDowntimes[gauge] += 1;
                if (gaugeDowntimes[gauge] >= gaugeMaxDowntime) {
                    _removeFromWhitelist(gauge, meta.call_id);
                }
            } else if (gauge_votes > max_votes) {
                currentVotingVotes[gauge] = max_votes;
                exceeded_votes += gauge_votes - max_votes;
                delete gaugeDowntimes[gauge];
            } else {
                valid_votes += gauge_votes;
                delete gaugeDowntimes[gauge];
            }

            counter += 1;
            pointer = gaugeWhitelist.next(gauge);
        }

        if (!finished) {
            (address gauge,) = pointer.get();
            this.countVotesStep{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                gauge, exceeded_votes, valid_votes, meta
            );
            return;
        }

        start_addr = address.makeAddrStd(address(this).wid, 0);
        this.normalizeVotesStep{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
            start_addr, 0, exceeded_votes, valid_votes, meta
        );
    }

    function normalizeVotesStep(
        address start_addr,
        uint128 overflow_votes,
        uint128 exceeded_votes,
        uint128 valid_votes,
        Callback.CallMeta meta
    ) external override {
        require (msg.sender == address(this), Errors.NOT_OWNER);
        tvm.rawReserve(_reserve(), 0);

        // if all 'excess' votes should go to treasury/next epoch, set valid votes to 0, so that we wont distribute anything among pools
        if (
            votingNormalizing == VotingNormalizingType.overflowTreasury ||
            votingNormalizing == VotingNormalizingType.overflowReserve
        ) {
            valid_votes = 0;
        }

        // if no valid votes/exceeded_votes, we dont need normalization
        if (valid_votes == 0 || exceeded_votes == 0) {
            // if exceeded_votes > 0, set all for treasury
            overflow_votes = exceeded_votes;
        } else {
            bool finished = false;
            uint32 counter = 0;
            uint128 max_votes = currentVotingTotalVotes * gaugeMaxVotesRatio / MAX_VOTES_RATIO;

            optional(address, uint128) pointer = currentVotingVotes.nextOrEq(start_addr);
            while (true) {
                if (!pointer.hasValue() || counter >= MAX_ITERATIONS_PER_COUNT) {
                    finished = !pointer.hasValue();
                    break;
                }

                (address gauge, uint128 gauge_votes) = pointer.get();
                if (gauge_votes < max_votes) {
                    uint128 bonus_votes = math.muldiv(gauge_votes, exceeded_votes, valid_votes);
                    gauge_votes += bonus_votes;
                    if (gauge_votes > max_votes) {
                        overflow_votes += gauge_votes - max_votes;
                    }
                    currentVotingVotes[gauge] = math.min(gauge_votes, max_votes);
                }

                counter += 1;
                pointer = currentVotingVotes.next(gauge);
            }

            if (!finished) {
                (address gauge,) = pointer.get();
                this.normalizeVotesStep{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                    gauge, overflow_votes, exceeded_votes, valid_votes, meta
                );
                return;
            }
        }

        emit VotingEnd(
            meta.call_id,
            currentVotingVotes,
            currentVotingTotalVotes,
            overflow_votes,
            currentEpoch,
            currentEpochStartTime,
            currentEpochEndTime
        );

        start_addr = address.makeAddrStd(address(this).wid, 0);
        mapping (address => uint128) distributed;
        this.distributeEpochQubesStep{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
            start_addr, overflow_votes, distributed, meta
        );
    }

    function distributeEpochQubesStep(
        address start_addr,
        uint128 overflow_votes,
        mapping (address => uint128) distributed,
        Callback.CallMeta meta
    ) external override {
        require (msg.sender == address(this), Errors.NOT_OWNER);
        tvm.rawReserve(_reserve(), 0);

        uint256 epoch_idx = currentEpoch - 2;
        uint128 to_distribute_total = distributionSchedule[epoch_idx];
        uint128 to_distribute_farming = math.muldiv(to_distribute_total, distributionScheme[FARMING_SCHEME], DISTRIBUTION_SCHEME_TOTAL) + emissionDebt;
        emissionDebt = 0;

        uint128 overflow_tokens = currentVotingTotalVotes > 0
            ? math.muldiv(to_distribute_farming, overflow_votes, currentVotingTotalVotes)
            : to_distribute_farming;

        emissionDebt = votingNormalizing == VotingNormalizingType.overflowReserve ? overflow_tokens : 0;
        uint128 treasury_bonus = votingNormalizing != VotingNormalizingType.overflowReserve ? overflow_tokens : 0;

        bool finished = false;
        uint32 counter = 0;

        TvmBuilder builder;
        builder.store(currentEpochStartTime, epochTime);
        TvmCell payload = builder.toCell();

        optional(address, uint128) pointer = currentVotingVotes.nextOrEq(start_addr);
        while (true) {
            if (!pointer.hasValue() || counter >= MAX_ITERATIONS_PER_COUNT) {
                finished = !pointer.hasValue();
                break;
            }

            (address gauge, uint128 gauge_votes) = pointer.get();

            uint128 qube_amount = math.muldiv(to_distribute_farming, gauge_votes, currentVotingTotalVotes);
            distributed[gauge] = qube_amount;

            qubeBalance -= qube_amount;
            _transferQubes(qube_amount, gauge, payload, meta.send_gas_to, MsgFlag.SENDER_PAYS_FEES);

            counter += 1;
            pointer = currentVotingVotes.next(gauge);
        }

        if (!finished) {
            (address gauge,) = pointer.get();
            this.distributeEpochQubesStep{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                gauge, overflow_votes, distributed, meta
            );
            return;
        }

        uint128 to_distribute_treasury = math.muldiv(to_distribute_total, distributionScheme[TREASURY_SCHEME], DISTRIBUTION_SCHEME_TOTAL);
        uint128 to_distribute_team = math.muldiv(to_distribute_total, distributionScheme[TEAM_SCHEME], DISTRIBUTION_SCHEME_TOTAL);
        to_distribute_treasury += treasury_bonus;

        treasuryTokens += to_distribute_treasury;
        teamTokens += to_distribute_team;
        distributionSupply -= to_distribute_total;

        currentVotingTotalVotes = 0;
        delete currentVotingVotes;

        emit EpochDistribution(
            meta.call_id,
            currentEpoch - 1,
            distributed,
            to_distribute_team,
            to_distribute_treasury,
            to_distribute_total,
            emissionDebt
        );

        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function getNormalizedVoting() external view returns (
        mapping (address => uint128) _votes,
        mapping (address => uint128) _normalizedVotes,
        mapping (address => uint128) _distribution,
        uint128 to_distribute_total,
        uint128 to_distribute_team,
        uint128 to_distribute_treasury,
        uint128 _emissionDebt
    ) {
        _votes = currentVotingVotes;
        _normalizedVotes = currentVotingVotes;
        uint128 min_votes = currentVotingTotalVotes * gaugeMinVotesRatio / MAX_VOTES_RATIO;
        uint128 max_votes = currentVotingTotalVotes * gaugeMaxVotesRatio / MAX_VOTES_RATIO;

        uint128 exceeded_votes = 0;
        uint128 valid_votes = 0;
        // count step
        for ((address gauge, uint128 vote) : _normalizedVotes) {
            if (vote < min_votes) {
                exceeded_votes += vote;
                _normalizedVotes[gauge] = 0;
            } else if (vote > max_votes) {
                _normalizedVotes[gauge] = max_votes;
                exceeded_votes += vote - max_votes;
            } else {
                valid_votes += vote;
            }
        }
        uint128 overflow_votes = 0;

        // if all 'excess' votes should go to treasury/next epoch, set valid votes to 0, so that we wont distribute anything among pools
        if (
            votingNormalizing == VotingNormalizingType.overflowTreasury ||
            votingNormalizing == VotingNormalizingType.overflowReserve
        ) {
            valid_votes = 0;
        }

        // no exceeded/valid votes, skip normalization
        if (valid_votes == 0 || exceeded_votes == 0) {
            overflow_votes = exceeded_votes;
        } else {
            // normalization step
            for ((address gauge, uint128 vote) : _normalizedVotes) {
                if (vote < max_votes) {
                    uint128 bonus_votes = math.muldiv(vote, exceeded_votes, valid_votes);
                    vote += bonus_votes;
                    if (vote > max_votes) {
                        overflow_votes += vote - max_votes;
                    }
                    _normalizedVotes[gauge] = math.min(vote, max_votes);
                }
            }
        }
        // distribution step
        to_distribute_total = distributionSchedule[currentEpoch - 1];
        uint128 to_distribute_farming = math.muldiv(to_distribute_total, distributionScheme[FARMING_SCHEME], DISTRIBUTION_SCHEME_TOTAL) + emissionDebt;

        uint128 overflow_tokens = currentVotingTotalVotes > 0
        ? math.muldiv(to_distribute_farming, overflow_votes, currentVotingTotalVotes)
        : to_distribute_farming;

        _emissionDebt = votingNormalizing == VotingNormalizingType.overflowReserve ? overflow_tokens : 0;
        uint128 treasury_bonus = votingNormalizing != VotingNormalizingType.overflowReserve ? overflow_tokens : 0;

        to_distribute_treasury = math.muldiv(to_distribute_total, distributionScheme[TREASURY_SCHEME], DISTRIBUTION_SCHEME_TOTAL);
        to_distribute_team = math.muldiv(to_distribute_total, distributionScheme[TEAM_SCHEME], DISTRIBUTION_SCHEME_TOTAL);
        to_distribute_treasury += treasury_bonus;

        for ((address gauge, uint128 vote) : _normalizedVotes) {
            uint128 qube_amount = math.muldiv(to_distribute_farming, vote, currentVotingTotalVotes);
            _distribution[gauge] = qube_amount;
        }
    }

    function withdrawTreasuryTokens(uint128 amount, address receiver, Callback.CallMeta meta) external onlyOwner {
        require (amount <= treasuryTokens, Errors.BAD_INPUT);

        tvm.rawReserve(_reserve(), 0);
        treasuryTokens -= amount;

        emit TreasuryWithdraw(meta.call_id, receiver, amount);
        TvmCell empty;
        qubeBalance -= amount;
        _transferQubes(amount, receiver, empty, meta.send_gas_to, MsgFlag.ALL_NOT_RESERVED);
    }

    function withdrawTeamTokens(uint128 amount, address receiver, Callback.CallMeta meta) external onlyOwner {
        require (amount <= teamTokens, Errors.BAD_INPUT);

        tvm.rawReserve(_reserve(), 0);
        teamTokens -= amount;

        emit TeamWithdraw(meta.call_id, receiver, amount);
        TvmCell empty;
        qubeBalance -= amount;
        _transferQubes(amount, receiver, empty, meta.send_gas_to, MsgFlag.ALL_NOT_RESERVED);
    }

    function withdrawPaymentTokens(uint128 amount, address receiver, Callback.CallMeta meta) external onlyOwner {
        require (amount <= whitelistPayments, Errors.BAD_INPUT);

        tvm.rawReserve(_reserve(), 0);
        whitelistPayments -= amount;

        emit PaymentWithdraw(meta.call_id, receiver, amount);
        TvmCell empty;
        qubeBalance -= amount;
        _transferQubes(amount, receiver, empty, meta.send_gas_to, MsgFlag.ALL_NOT_RESERVED);
    }
}
