pragma ever-solidity ^0.62.0;


import "broxus-token-contracts/contracts/interfaces/ITokenRootUpgradeable.tsol";
import "broxus-token-contracts/contracts/interfaces/ITokenWalletUpgradeable.tsol";
import "broxus-token-contracts/contracts/interfaces/IAcceptTokensTransferCallback.tsol";
import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "../../../libraries/Errors.tsol";
import "../../../libraries/Callback.tsol";
import "../../../gauge/interfaces/IGaugeAccount.tsol";
import "./VoteEscrowEpochVoting.tsol";


abstract contract VoteEscrowBase is VoteEscrowEpochVoting {
    function transferOwnership(address new_owner, Callback.CallMeta meta) external override onlyOwner {
        tvm.rawReserve(_reserve(), 0);

        emit NewPendingOwner(meta.call_id, new_owner);
        pendingOwner = new_owner;
        meta.send_gas_to.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
    }

    function acceptOwnership(Callback.CallMeta meta) external {
        require (msg.sender == pendingOwner, Errors.NOT_OWNER);
        tvm.rawReserve(_reserve(), 0);

        emit NewOwner(meta.call_id, owner, pendingOwner);
        owner = pendingOwner;
        pendingOwner = address.makeAddrNone();
        meta.send_gas_to.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
    }

    function receiveTokenWalletAddress(address wallet) external override {
        require (msg.sender == qube);
        qubeWallet = wallet;
    }

    function onAcceptTokensTransfer(
        address,
        uint128 amount,
        address sender,
        address,
        address remainingGasTo,
        TvmCell payload
    ) external override {
        require (msg.sender == qubeWallet, Errors.NOT_TOKEN_WALLET);
        tvm.rawReserve(_reserve(), 0);

        (
            uint8 deposit_type,
            uint32 nonce,
            uint32 call_id,
            TvmCell additional_payload,
            bool correct
        ) = decodeTokenTransferPayload(payload);

        // common cases
        bool exception = !correct || paused || !initialized || emergency || msg.value < Gas.MIN_MSG_VALUE || uint8(deposit_type) > 2;

        // specific cases
        if (deposit_type == uint8(DepositType.userDeposit)) {
            (address deposit_owner, uint32 lock_time) = decodeDepositPayload(additional_payload);
            exception = exception || lock_time < qubeMinLockTime || lock_time > qubeMaxLockTime;
            sender = deposit_owner;

            if (!exception) {
                // deposit logic
                uint128 ve_minted = calculateVeMint(amount, lock_time);
                deposit_nonce += 1;
                pending_deposits[deposit_nonce] = PendingDeposit(
                    deposit_owner, amount, ve_minted, lock_time, Callback.CallMeta(call_id, nonce, remainingGasTo)
                );

                address ve_account_addr = getVoteEscrowAccountAddress(deposit_owner);
                IVoteEscrowAccount(ve_account_addr).processDeposit{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                    deposit_nonce, amount, ve_minted, lock_time, Callback.CallMeta(call_id, nonce, remainingGasTo)
                );
                return;
            }
        } else if (deposit_type == uint8(DepositType.whitelist)) {
            address whitelist_addr = decodeWhitelistPayload(additional_payload);
            exception = exception || gaugeWhitelist[whitelist_addr] == true || amount < gaugeWhitelistPrice || currentVotingStartTime != 0;
            if (!exception) {
                // whitelist address
                qubeBalance += amount;
                whitelistPayments += amount;
                _addToWhitelist(whitelist_addr, call_id);
                _sendCallbackOrGas(sender, nonce, true, remainingGasTo);
                return;
            }
        } else if (deposit_type == uint8(DepositType.adminDeposit)) {
            if (!exception) {
                emit DistributionSupplyIncrease(call_id, amount);
                // tokens for distribution
                qubeBalance += amount;
                distributionSupply += amount;
                _sendCallbackOrGas(sender, nonce, true, remainingGasTo);
                return;
            }
        }

        if (exception) {
            emit DepositRevert(call_id, sender, amount);
            // if payload assembled correctly, send nonce, otherwise send payload we got with this transfer
            payload = correct ? _makeCell(nonce) : payload;
            _transferQubes(amount, sender, payload, remainingGasTo, MsgFlag.ALL_NOT_RESERVED);
        }
    }

    function revertDeposit(address user, uint32 deposit_nonce) external override onlyVoteEscrowAccount(user) {
        tvm.rawReserve(_reserve(), 0);
        PendingDeposit deposit = pending_deposits[deposit_nonce];
        delete pending_deposits[deposit_nonce];

        emit DepositRevert(deposit.meta.call_id, deposit.user, deposit.amount);
        _transferQubes(
            deposit.amount, deposit.user, _makeCell(deposit.meta.nonce), deposit.meta.send_gas_to, MsgFlag.ALL_NOT_RESERVED
        );
    }

    function finishDeposit(address user, uint64 deposit_key, uint32 deposit_nonce) external override onlyVoteEscrowAccount(user) {
        tvm.rawReserve(_reserve(), 0);
        PendingDeposit deposit = pending_deposits[deposit_nonce];
        delete pending_deposits[deposit_nonce];

        emit Deposit(deposit.meta.call_id, deposit.user, deposit.amount, deposit.ve_amount, deposit.lock_time, deposit_key);
        updateAverage();
        qubeBalance += deposit.amount;
        veQubeBalance += deposit.ve_amount;

        _sendCallbackOrGas(user, deposit.meta.nonce, true, deposit.meta.send_gas_to);
    }

    function withdraw(Callback.CallMeta meta) external view onlyActive {
        require (msg.value >= Gas.MIN_MSG_VALUE, Errors.LOW_MSG_VALUE);
        tvm.rawReserve(_reserve(), 0);

        address ve_acc_addr = getVoteEscrowAccountAddress(msg.sender);
        IVoteEscrowAccount(ve_acc_addr).processWithdraw{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(meta);
    }

    function revertWithdraw(address user, Callback.CallMeta meta) external override onlyVoteEscrowAccount(user) {
        tvm.rawReserve(_reserve(), 0);

        emit WithdrawRevert(meta.call_id, user);
        _sendCallbackOrGas(user, meta.nonce, false, meta.send_gas_to);
    }

    function finishWithdraw(address user, uint128 unlockedQubes, Callback.CallMeta meta) external override onlyVoteEscrowAccount(user) {
        tvm.rawReserve(_reserve(), 0);

        updateAverage();
        emit Withdraw(meta.call_id, user, unlockedQubes);

        qubeBalance -= unlockedQubes;
        _transferQubes(unlockedQubes, user, _makeCell(meta.nonce), meta.send_gas_to, MsgFlag.ALL_NOT_RESERVED);
    }

    function burnVeQubes(address user, uint128 expiredVeQubes, uint64[] expiredDeposits) external override onlyVoteEscrowAccount(user) {
        tvm.rawReserve(_reserve(), 0);

        updateAverage();
        veQubeBalance -= expiredVeQubes;
        emit VeQubesBurn(user, expiredVeQubes, expiredDeposits);

        user.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function setQubeLockTimeLimits(uint32 new_min, uint32 new_max, Callback.CallMeta meta) external override onlyOwner {
        tvm.rawReserve(_reserve(), 0);

        qubeMinLockTime = new_min;
        qubeMaxLockTime = new_max;

        emit NewQubeLockLimits(meta.call_id, new_min, new_max);
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function setPause(bool new_state, Callback.CallMeta meta) external onlyOwner {
        tvm.rawReserve(_reserve(), 0);

        paused = new_state;
        emit Pause(meta.call_id, new_state);

        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function setEmergency(bool new_state, Callback.CallMeta meta) external onlyOwner {
        tvm.rawReserve(_reserve(), 0);

        emergency = new_state;
        emit Emergency(meta.call_id, new_state);

        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function setVotingNormalizingType(VotingNormalizingType _new_type, Callback.CallMeta meta) external onlyOwner {
        tvm.rawReserve(_reserve(), 0);

        votingNormalizing = _new_type;
        emit NormalizingTypeUpdate(meta.call_id, _new_type);

        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function setWhitelistPrice(uint128 new_price, Callback.CallMeta meta) external override onlyOwner {
        tvm.rawReserve(_reserve(), 0);

        gaugeWhitelistPrice = new_price;

        emit WhitelistPriceUpdate(meta.call_id, new_price);
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function addToWhitelist(address gauge, Callback.CallMeta meta) external onlyOwner {
        // cant add gauge to whitelist during voting
        require (currentVotingStartTime == 0, Errors.BAD_INPUT);
        tvm.rawReserve(_reserve(), 0);

        _addToWhitelist(gauge, meta.call_id);
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function removeFromWhitelist(address gauge, Callback.CallMeta meta) external onlyOwner {
        // cant remove gauge from whitelist during voting
        require (currentVotingStartTime == 0, Errors.BAD_INPUT);
        tvm.rawReserve(_reserve(), 0);

        _removeFromWhitelist(gauge, meta.call_id);
        meta.send_gas_to.transfer(0, false, MsgFlag.ALL_NOT_RESERVED);
    }

    function approveGaugeDAO(address gauge) external onlyOwnerOrDao {
        require (gaugeWhitelist[gauge] == true, Errors.GAUGE_NOT_WHITELISTED);

        gaugeDaoApproved[gauge] = true;
        emit GaugeDAOApprove(0, gauge);
    }

    function removeGaugeDAO(address gauge) external onlyOwnerOrDao {
        require (gaugeDaoApproved.exists(gauge), Errors.GAUGE_NOT_WHITELISTED);

        // remove completely
        _removeFromWhitelist(gauge, 0);
    }

    function getVeAverage(uint32 nonce) external override {
        tvm.rawReserve(_reserve(), 0);

        updateAverage();

        IGaugeAccount(msg.sender).receiveVeAverage{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
            nonce, veQubeBalance, veQubeAverage, veQubeAveragePeriod
        );
    }

    function calculateAverage() public view returns (
        uint32 _lastUpdateTime, uint128 _veQubeBalance, uint128 _veQubeAverage, uint32 _veQubeAveragePeriod
    ) {
        if (now <= lastUpdateTime || lastUpdateTime == 0) {
            // already updated on this block or this is our first update
            return (now, veQubeBalance, veQubeAverage, veQubeAveragePeriod);
        }

        uint32 last_period = now - lastUpdateTime;
        _veQubeAverage = (veQubeAverage * veQubeAveragePeriod + veQubeBalance * last_period) / (veQubeAveragePeriod + last_period);
        _veQubeAveragePeriod = veQubeAveragePeriod + last_period;
        _lastUpdateTime = now;
        _veQubeBalance = veQubeBalance;
    }

    function updateAverage() internal {
        (lastUpdateTime, veQubeBalance, veQubeAverage, veQubeAveragePeriod) = calculateAverage();
    }

    onBounce(TvmSlice slice) external view {
        tvm.accept();

        uint32 functionId = slice.decode(uint32);
        // if processing failed - contract was not deployed. Deploy and try again
        if (functionId == tvm.functionId(IVoteEscrowAccount.processDeposit)) {
            tvm.rawReserve(_reserve(), 0);

            uint32 _deposit_nonce = slice.decode(uint32);
            PendingDeposit deposit = pending_deposits[_deposit_nonce];
            address gauge_account_addr = deployVoteEscrowAccount(deposit.user);

            IVoteEscrowAccount(gauge_account_addr).processDeposit{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                _deposit_nonce, deposit.amount, deposit.ve_amount, deposit.lock_time, deposit.meta
            );
        }
    }
}
