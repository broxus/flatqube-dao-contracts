pragma ever-solidity ^0.62.0;


import "../../../libraries/Gas.tsol";
import "../../../libraries/Errors.tsol";
import "../../interfaces/IVoteEscrow.tsol";
import "../../interfaces/IVoteEscrowAccount.tsol";
import "../../../gauge/interfaces/IGaugeAccount.tsol";

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "./VoteEscrowAccountDAO.tsol";


abstract contract VoteEscrowAccountBase is VoteEscrowAccountDAO {
    function onDeployRetry(TvmCell, TvmCell, address sendGasTo) external view onlyVoteEscrowOrSelf functionID(0x23dc4360){
        tvm.rawReserve(_reserve(), 0);
        sendGasTo.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
    }

    function processVoteEpoch(
        uint32 voteEpoch, mapping (address => uint128) votes, Callback.CallMeta meta
    ) external override onlyVoteEscrowOrSelf {
        require (lastEpochVoted < voteEpoch, Errors.ALREADY_VOTED);

        tvm.rawReserve(_reserve(), 0);

        // check gas only at beginning
        if (msg.sender == voteEscrow && msg.value < calculateMinGas()) {
            IVoteEscrow(voteEscrow).revertVote{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(user, meta);
            return;
        }

        bool update_finished = _syncDeposits(now);
        if (!update_finished) {
            this.processVoteEpoch{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                voteEpoch, votes, meta
            );
            return;
        }

        uint128 totalVotes = 0;
        for ((, uint128 vote_value) : votes) {
            totalVotes += vote_value;
        }
        if (veQubeBalance < totalVotes) {
            // soft fail, because ve qubes could be burned while syncing and we want to return gas to user and notify him
            IVoteEscrow(voteEscrow).revertVote{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(user, meta);
            return;
        }

        lastEpochVoted = voteEpoch;
        IVoteEscrow(voteEscrow).finishVote{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(user, votes, meta);
    }

    function processWithdraw(Callback.CallMeta meta) external override onlyVoteEscrowOrSelf {
        tvm.rawReserve(_reserve(), 0);

        // check gas only at beginning + check user did not vote
        if (msg.sender == voteEscrow && (msg.value < calculateMinGas() || _lockedTokens() > 0)) {
            IVoteEscrow(msg.sender).revertWithdraw{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(user, meta);
            return;
        }

        bool update_finished = _syncDeposits(now);
        // continue update in next message with same parameters
        if (!update_finished) {
            this.processWithdraw{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(meta);
            return;
        }

        qubeBalance -= unlockedQubes;
        uint128 _withdraw_qubes = unlockedQubes;
        unlockedQubes = 0;

        IVoteEscrow(voteEscrow).finishWithdraw{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(user, _withdraw_qubes, meta);
    }

    function processDeposit(
        uint32 deposit_nonce,
        uint128 qube_amount,
        uint128 ve_amount,
        uint32 lock_time,
        Callback.CallMeta meta
    ) external override onlyVoteEscrowOrSelf {
        tvm.rawReserve(_reserve(), 0);

        // check gas only at beginning
        if (msg.sender == voteEscrow && msg.value < calculateMinGas()) {
            IVoteEscrow(msg.sender).revertDeposit{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(user, deposit_nonce);
            return;
        }

        bool update_finished = _syncDeposits(now);
        // continue update in next message with same parameters
        if (!update_finished) {
            this.processDeposit{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
                deposit_nonce, qube_amount, ve_amount, lock_time, meta
            );
            return;
        }

        uint64 deposit_key = _saveDeposit(qube_amount, ve_amount, lock_time);
        IVoteEscrow(voteEscrow).finishDeposit{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(user, deposit_key, deposit_nonce);
    }

    // Update averages up to current moment taking into account expired deposits
    // @param callback_receiver - address that will receive callback
    // @param callback_nonce - nonce that will be sent with callback
    // @param sync_time - timestamp. Ve stats will be updated up to this moment
    function getVeAverage(address callback_receiver, uint32 callback_nonce, uint32 sync_time) external override {
        // not enough gas to update all our entities!
        if (msg.value < calculateMinGas() && msg.sender == callback_receiver) {
            IGaugeAccount(msg.sender).revertAction{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(callback_nonce);
            return;
        }
        require (msg.sender == address(this) || msg.sender == callback_receiver, Errors.BAD_SENDER);
        tvm.rawReserve(_reserve(), 0);

        // update ve stats before sending callback
        bool update_finished = _syncDeposits(sync_time);
        // continue update in next message with same parameters
        if (!update_finished) {
            this.getVeAverage{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(callback_receiver, callback_nonce, sync_time);
            return;
        }
        IGaugeAccount(callback_receiver).receiveVeAccAverage{value: 0, flag: MsgFlag.ALL_NOT_RESERVED}(
            callback_nonce, veQubeBalance, veQubeAverage, veQubeAveragePeriod
        );
    }
}
